<job id="ZTICScript_Log">
	<script language="VBScript" src="ZTIUtility.vbs"/>
	<script language="VBScript">

' // ***************************************************************************
' // 
' // Copyright (c) Microsoft Corporation.  All rights reserved.
' // 
' // Microsoft Deployment Toolkit Extensions http://MDTEx.codeplex.com
' //
' // File:      ZTICScript_Log.wsf
' // 
' // Version:   6.2.5019.x
' // 
' // Purpose:   Used to capture script output to log file in case of script crashes.
' // 
' // Usage:     cscript.exe [//nologo] ZTICScript_Log.wsf [/debug:true] 
' //                 /ScriptName:ScriptName /LogFile:LogFile /ErrorLevel:ErrorLevel
' // 
' // ***************************************************************************

Option Explicit
RunNewInstance


'//----------------------------------------------------------------------------
'//  Main Class
'//----------------------------------------------------------------------------

Class ZTICScript_Log

	'//----------------------------------------------------------------------------
	'//  Class instance variable declarations
	'//----------------------------------------------------------------------------

	Public sLog
	Public sScr
	Public sErr
	

	'//----------------------------------------------------------------------------
	'//  Constructor to initialize needed global objects
	'//----------------------------------------------------------------------------

	Private Sub Class_Initialize

		sLog = oEnvironment.Item("LogFile")
		sScr = oEnvironment.Item("ScriptName")
		sErr = oEnvironment.Item("ErrorLevel")

	End Sub
	

	'//----------------------------------------------------------------------------
	'//  Main routine
	'//----------------------------------------------------------------------------

	Function Main
	
		' Local Variables

		Dim oLog
		Dim sLine


		' Main script processing
		
		TestAndFail sScr <> "", 11001, "Check environment variable: ScriptName"
		TestAndFail sErr <> "", 11002, "Check environment variable: ErrorLevel"
		TestAndFail sLog <> "", 11003, "Check environment variable: LogFile"

		oLogging.CreateEntry "The following script returned an error: " & sScr & "  With an error return code of: " & sErr, LogTypeInfo
		oLogging.CreateEntry "Log results were written to: " & sLog , LogTypeInfo

		TestAndFail oFSO.FileExists(sLog), 11004, "Check for Log File: " & sLog


		' Now write to the correct log file

		oLogging.Component = oFSO.GetBaseName(sScr)
		oLogging.LogFile = oLogging.Component & ".log"
		
		oLogging.CreateEntry "The following script returned an error: " & sScr & "  With an error return code of: " & sErr, LogTypeError
		oLogging.CreateEntry "Log results were written to: " & sLog , LogTypeError
		

		' Open the log file and import the results

		Set oLog = oFSO.OpenTextFile( oEnvironment.Item("LogFile") )
		While not oLog.AtEndOfStream		
			sLine = oLog.ReadLine
			If trim(sLine) <> "" then
				oLogging.CreateEntry "        #> " & sLine , LogTypeError
			End if
		Wend
		oLog.Close
		Set oLog = Nothing


		Main = SUCCESS

	End Function


End class


	</script>
</job>
